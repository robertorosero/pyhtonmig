dnl Process this file with autoconf to produce a configure script.
AC_INIT(Include/object.h)
AC_CONFIG_HEADER(Include/config.h)

echo checking for --with-solaris
AC_WITH(solaris, [AC_DEFINE(SOLARIS)])

# checks for alternative programs
AC_PROG_CC
AC_PROG_RANLIB

# checks for UNIX variants that set C preprocessor variables
AC_AIX
AC_ISC_POSIX
AC_MINIX
dnl DYNIX test runs compile so must be last
AC_DYNIX_SEQ

# checks for header files
AC_STDC_HEADERS
AC_HAVE_HEADERS(dlfcn.h signal.h stdarg.h stdlib.h unistd.h utime.h sys/param.h sys/select.h sys/times.h sys/un.h sys/utsname.h)
AC_DIR_HEADER

# checks for typedefs
AC_MODE_T
AC_OFF_T
AC_PID_T
AC_RETSIGTYPE
AC_SIZE_T
AC_UID_T

# checks for libraries
AC_HAVE_LIBRARY(dl)
AC_HAVE_LIBRARY(socket)
AC_HAVE_LIBRARY(nsl)

echo checking for --with-readline
AC_WITH(readline, [AC_DEFINE(WITH_READLINE)
if test "$withval" != 1; then LIBS="$LIBS -L$withval"; fi
LIBS="$LIBS -lreadline -ltermcap"])

echo checking for --with-thread
AC_WITH(thread, [
AC_HAVE_LIBRARY(mpc, [AC_DEFINE(USE_THREAD)
LIBS="$LIBS -lmpc"
LIBOBJS="$LIBOBJS thread.o"])
AC_HAVE_LIBRARY(thread, [AC_DEFINE(USE_THREAD)
LIBS="$LIBS -lthread"
LIBOBJS="$LIBOBJS thread.o"])
])

# checks for library functions
AC_HAVE_FUNCS(clock ftime gettimeofday getpeername getpgrp getwd lstat readlink readline select setsid setpgid setpgrp siginterrupt symlink tcgetpgrp tcsetpgrp times uname waitpid)
AC_REPLACE_FUNCS(dup2 getcwd strerror strtod memmove)
AC_FUNC_CHECK(getpgrp, AC_COMPILE_CHECK([argument to getpgrp], [#include <unistd.h>], [getpgrp(0);], AC_DEFINE(GETPGRP_HAVE_ARG)))

# checks for structures
AC_TIME_WITH_SYS_TIME
AC_STRUCT_TM
AC_TIMEZONE

# checks for compiler characteristics
AC_CONST
echo checking for prototypes
AC_TEST_PROGRAM([int foo(int x){return 0;} int main(){return foo(10);}], AC_DEFINE(HAVE_PROTOTYPES) have_prototypes=1)
if test "$have_prototypes"; then
AC_COMPILE_CHECK(["bad exec* prototypes"], [#include <unistd.h>], [char *const*t;execve("@",t,t);], , AC_DEFINE(BAD_EXEC_PROTOTYPES))
fi

# checks for system services
# (none yet)

# other checks for UNIX variants
AC_IRIX_SUN
AC_XENIX_DIR

# generate output files
AC_OUTPUT(Objects/Makefile Parser/Makefile Python/Makefile Modules/Makefile)
